
plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
    id "com.gorylenko.gradle-git-properties" version "2.2.0" apply false
    id "com.pasam.gradle.buildinfo" version "0.1.3" apply false
    id "com.google.cloud.tools.jib" version "1.8.0" apply false
}

allprojects {
    repositories {
        maven { url 'https://maven-central.storage.googleapis.com' } // google mirror of maven central
        maven { url "https://jcenter.bintray.com" }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    configurations {
        // for dependencies that are needed for development only
        developmentOnly
    }

    java {
        sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
        targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
    }

    // use JUnit 5 platform
    test {
        useJUnitPlatform()
        // disable HTML test reports for individual subprojects
        reports.html.enabled = false

        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
        failFast = true

        classpath += configurations.developmentOnly
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs.add('-parameters')
    }

}

configure(subprojects.findAll { ['mylib'].contains(it.name)}) {
    apply plugin: 'java-library'
}

configure(subprojects.findAll { ['myapp-a','myapp-b'].contains(it.name)}) {

    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "com.google.cloud.tools.jib"
    apply plugin: 'application'

    shadowJar {
        mergeServiceFiles()
    }

    run.classpath += configurations.developmentOnly
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
